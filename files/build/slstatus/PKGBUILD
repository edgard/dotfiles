# $Id$
# Maintainer: Edgard Castro <edgardcastro@gmail.com>

pkgname=slstatus-git
pkgver=r552.b14e039
pkgrel=1
epoch=999
pkgdesc='A status monitor for window managers'
url='http://tools.suckless.org/slstatus'
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=('slstatus')
source=("git+http://git.suckless.org/${pkgname%-git}"
	"config.h")
sha256sums=('SKIP'
	'SKIP')

pkgver() {
	cd "${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cp config.h "${pkgname%-git}/config.h"
}

build() {
	cd "${pkgname%-git}"
	make X11INC='/usr/include/X11' X11LIB='/usr/lib/X11'
}

package() {
	cd "${pkgname%-git}"
	make PREFIX='/usr' DESTDIR="${pkgdir}" install
	install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname%-git}/LICENSE"
	install -m644 -D README "${pkgdir}/usr/share/doc/${pkgname%-git}/README"
}
