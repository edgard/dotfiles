#!/usr/bin/env zsh

# Source Catppuccin FrappÃ© theme variables
source "${HOME}/.zsh/_theme.zsh"

##############################################################################
# Type Declarations (Unique, Ordered Arrays)
##############################################################################

typeset -gU path PATH
typeset -gUT FPATH fpath

##############################################################################
# macOS System Path Setup
##############################################################################

if [[ "$OSTYPE" == darwin* ]] && [[ -x "/usr/libexec/path_helper" ]]; then
    eval "$(/usr/libexec/path_helper -s)"
fi

##############################################################################
# Homebrew Environment Setup
##############################################################################

function load_homebrew() {
    local brew_path=""

    if [[ "$OSTYPE" == darwin* ]]; then
        for bp in /opt/homebrew/bin/brew /usr/local/bin/brew; do
            [[ -x "$bp" ]] && { brew_path="$bp"; break; }
        done
    else
        for bp in /home/linuxbrew/.linuxbrew/bin/brew "$HOME/.linuxbrew/bin/brew"; do
            [[ -x "$bp" ]] && { brew_path="$bp"; break; }
        done
    fi

    [[ -n "$brew_path" ]] && eval "$("$brew_path" shellenv)"
}

load_homebrew
unset -f load_homebrew

##############################################################################
# User Binaries
##############################################################################

typeset -aU user_paths=(
    "${HOME}/.go/bin"
    "${HOME}/.cargo/bin"
    "${HOME}/.npm-global/bin"
    "${HOME}/.gem/bin"
    "${HOME}/.local/bin"
    "${HOME}/Documents/Projects/dev-utils/bin"
)

for user_path in "${user_paths[@]}"; do
    [[ -d "$user_path" ]] && path=("$user_path" $path)
done

unset user_paths user_path

##############################################################################
# Language Environment Variables
##############################################################################

command -v go >/dev/null 2>&1 && export GOPATH="${HOME}/.go"
command -v gem >/dev/null 2>&1 && export GEM_HOME="${HOME}/.gem"
[[ -f "${HOME}/.cargo/env" ]] && source "${HOME}/.cargo/env"
[[ -d "${HOME}/.npm-global" ]] && export NPM_CONFIG_PREFIX="${HOME}/.npm-global"

##############################################################################
# Modular Zsh Configurations
##############################################################################

typeset -a config_files=("${HOME}"/.zsh/<->-*.zsh(.N))
for config_file in "${config_files[@]}"; do
    source "${config_file}" || printf "Error: Failed to source %s\n" "${config_file##*/}" >&2
done

unset config_files config_file
